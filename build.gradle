plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.diffplug.spotless' version '7.0.2' apply false
    id 'checkstyle'
}

allprojects {
    group = 'com.barofarm'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    // NOTE:
    // - 각 서비스 모듈(auth/buyer/seller/order/support)은 자체 build.gradle 로
    //   plugins, 의존성, JDK 설정 등을 모두 관리하는 자립형 MSA 서비스로 구성되어 있다.
    // - 이 루트 build.gradle 은 멀티모듈(단일 레포) 구조의 루트 프로젝트를 유지하고,
    //   전역 코드 스타일 규칙(spotless/checkstyle)과 공통 lint/format 태스크만 제공하는
    //   "얇은 부모" 역할로 남겨둔다.
    //
    // Spotless - 코드 자동 포맷팅 (like Python black)
    // 각 서브프로젝트에서 com.diffplug.spotless 플러그인을 사용하는 경우에만 설정 적용
    plugins.withId('com.diffplug.spotless') {
        spotless {
            java {
                target 'src/*/java/**/*.java'
                importOrder()
                removeUnusedImports()
                // eclipse() 제거: 메서드 체이닝과 파라미터 포맷팅이 자동으로 변경되지 않도록
                leadingTabsToSpaces(4)  // 탭을 4칸 스페이스로 변환
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    // ============================================
    // Checkstyle - 코드 스타일 검사 (lint)
    // ============================================
    // 각 서브프로젝트에서 checkstyle 플러그인을 사용하는 경우에만 설정 적용
    plugins.withId('checkstyle') {
        checkstyle {
            toolVersion = '10.21.4'
            configFile = rootProject.file('config/checkstyle/checkstyle.xml')
            configProperties = [
                'suppressionFile': rootProject.file('config/checkstyle/suppressions.xml').absolutePath
            ]
            ignoreFailures = false
            maxWarnings = 0
        }

        tasks.withType(Checkstyle).configureEach {
            reports {
                xml.required = true
                html.required = true
            }
        }
    }
}

// ============================================
// 커밋 전 검사용 Task
// ============================================
tasks.register('lint') {
    group = 'verification'
    description = '코드 품질 검사 (spotless + checkstyle)'
    // dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessCheck' } }
    // dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'checkstyleMain' } }
    // gateway 모듈은 의존성 문제로 제외
    def projectsToLint = subprojects.findAll { it.path != ':baro-cloud:gateway' }
    dependsOn projectsToLint.collect { it.tasks.matching { t -> t.name == 'spotlessCheck' } }
    dependsOn projectsToLint.collect { it.tasks.matching { t -> t.name == 'checkstyleMain' } }
}

tasks.register('format') {
    group = 'verification'
    description = '코드 자동 포맷용'
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessApply' } }
}
