plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.diffplug.spotless' version '7.0.2' apply false
    id 'checkstyle'
}

allprojects {
    group = 'com.barofarm'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'checkstyle'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
            // CI/CD 환경(GitHub Actions 등)에서는 vendor 체크를 건너뛰어
            // temurin(OpenJDK) 등 모든 JDK를 허용
            // 로컬 개발 환경에서만 JetBrains JDK 사용
            if (!System.getenv('CI') && !System.getenv('GITHUB_ACTIONS')) {
                vendor = JvmVendorSpec.JETBRAINS
            }
        }
    }

    ext {
        set('springCloudVersion', "2025.0.0")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'com.h2database:h2'
        testImplementation 'io.rest-assured:rest-assured:5.5.5'
        testImplementation "org.springframework.boot:spring-boot-testcontainers"
        testImplementation "org.testcontainers:junit-jupiter"
        testImplementation "org.testcontainers:testcontainers"
        testImplementation platform("org.testcontainers:testcontainers-bom:1.19.7")

        // Mockito, AssertJ
        testImplementation 'org.mockito:mockito-junit-jupiter'
        testImplementation 'org.assertj:assertj-core'
        testImplementation platform("org.testcontainers:testcontainers-bom:1.19.7")

        // swagger
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // ============================================
    // Spotless - 코드 자동 포맷팅 (like Python black)
    // ============================================
    spotless {
        java {
            target 'src/*/java/**/*.java'
            importOrder()
            removeUnusedImports()
            leadingTabsToSpaces(4)
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    // ============================================
    // Checkstyle - 코드 스타일 검사 (lint)
    // ============================================
    checkstyle {
        toolVersion = '10.21.4'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configDirectory = rootProject.file('config/checkstyle')
        configProperties = [
            'suppressionFile': rootProject.file('config/checkstyle/suppressions.xml').absolutePath
        ]
        ignoreFailures = false
        maxWarnings = 0
    }

    tasks.withType(Checkstyle).configureEach {
        reports {
            xml.required = true
            html.required = true
        }
    }
}

// ============================================
// 커밋 전 검사용 Task
// ============================================
tasks.register('lint') {
    group = 'verification'
    description = '코드 품질 검사 (spotless + checkstyle)'
    // dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessCheck' } }
    // dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'checkstyleMain' } }
    // gateway 모듈은 의존성 문제로 제외
    def projectsToLint = subprojects.findAll { it.path != ':baro-cloud:gateway' }
    dependsOn projectsToLint.collect { it.tasks.matching { t -> t.name == 'spotlessCheck' } }
    dependsOn projectsToLint.collect { it.tasks.matching { t -> t.name == 'checkstyleMain' } }
}

tasks.register('format') {
    group = 'verification'
    description = '코드 자동 포맷용'
    dependsOn subprojects.collect { it.tasks.matching { t -> t.name == 'spotlessApply' } }
}

// 서비스 모듈은 jar만 생성 (라이브러리로 사용)
// baro-cloud 빌드 제외 여부 나중에
[
    ':baro-cloud:gateway',
    ':baro-cloud:config',
    ':baro-cloud:eureka'
].each { servicePath ->
    project(servicePath) {
        bootJar.enabled = true
        jar.enabled = true
    }
}
